import "chicago-crimes-duckdb-2022.malloy"

-- Crimes Report Dashboard
query: crimes_dashboard is crimes -> {
  nest:
    Reports
    Domestic_Reports
    Arrests
}

-- Crime Reports by Primary Type
query: crimes_by_primary_type_bar_chart is
  crimes -> crimes_by_primary_type

-- Crime Reports by Description
query: crimes_by_description_bar_chart is
  crimes -> crimes_by_description { limit: 20 }

-- Crime Reports by Primary Type with nested Description
query: crimes_by_primary_type_summary is
  crimes -> crimes_by_primary_type + {
    nest: crimes_by_description_bar_chart is
      crimes_by_description { limit: 10 }
    }

-- Crime Reports by Location Description
query: crimes_by_location_description_bar_chart is
  crimes -> crimes_by_location_description { limit: 20 }

-- Crime Reports by Month
query: crimes_by_month_line_chart is
  crimes -> crimes_by_month

-- Crime Reports by Primary Type per Month
query: crimes_by_primary_type is
  crimes -> {
    nest: crimes_by_month is {
      group_by: `Month` is `Date`.month
      aggregate: Reports
      group_by: `Primary Type`
    }
  }

-- Crime Reports by Primary Type with
-- Monthly Counts Summary and Top Locations
query: crimes_by_type_month_and_location is crimes -> {
  group_by: `Primary Type`
  aggregate:
    Reports
    percent_of_reports is Reports/all(Reports) * 100
  nest:
    crimes_by_month_line_chart
    top_locations is {
      group_by: `Location Description`
      aggregate:
        Reports
        percent_of_reports is Reports/all(Reports)
      limit: 15
    }
  limit: 20
}

-- Bike Thefts
query: bike_thefts is crimes -> {
  where: Description ~ '%BIKE%'
  group_by: `Primary Type`
  aggregate:
    Reports
  nest:
    crimes_by_month_line_chart
    top_locations is {
      group_by: `Location Description`
      aggregate:
        Reports
        percent_of_reports is Reports/all(Reports) * 100
      limit: 15
    }
    crimes_by_description is {
      group_by: Description
      aggregate: Reports
      limit: 10
    }
  limit: 20
}

-- Weapons Violations
query: weapons_violations is crimes -> {
  where: `Primary Type` ~ '%WEAPONS%'
  group_by: `Primary Type`
  aggregate:
    Reports
  nest:
    crimes_by_month_line_chart
    top_locations is {
      group_by: `Location Description`
      aggregate:
        Reports
        percent_of_reports is Reports/all(Reports) * 100
      limit: 15
    }
    crimes_by_description is {
      group_by: Description
      aggregate: Reports
      limit: 10
    }
  limit: 20
}
