source: crimes is
  table('duckdb:../data/crimes-2022.parquet') {
    primary_key: ID
    measure: Reports is count()
    measure: Arrests is Reports {
      where: Arrest = true
    }
    measure: Domestic_Reports is Reports {
      where: Domestic = true
    }
    query: crimes_by_primary_type is {
      group_by: `Primary Type`
      aggregate: Reports
    }
    query: crimes_by_description is {
      group_by: Description,
      aggregate: Reports
    }
  }

-- Total Crime Reports
query: crime_reports is crimes -> {
  aggregate: Reports
}

-- Arrests
query: arrests is crimes -> {
  aggregate: Arrests
}

-- Domestic
query: domestic_crime_reports is crimes -> {
  aggregate: Domestic_Reports
}

-- Crime Reports by Primary Type and Description
query: crimes_by_primary_type_and_description is crimes -> {
  group_by: `Primary Type`, Description
  aggregate: Reports
}

-- Crime Reports by Primary Type with nested Description
query: crimes_by_primary_type_with_description is
  crimes -> crimes_by_primary_type + {
    nest: crimes_by_description
  }

-- Crime Reports by Location Description
query: crimes_by_location_description is crimes -> {
  group_by: `Location Description`
  aggregate: Reports
}

-- Crime Reports by Community Area
-- Chicago community areas:
-- https://en.wikipedia.org/wiki/Community_areas_in_Chicago
query: crimes_by_community_area is crimes -> {
  group_by: `Community Area`
  aggregate: Reports
}

-- Chicago Districts and Beats:
-- https://news.wttw.com/sites/default/files/Map%20of%20Chicago%20Police%20Districts%20and%20Beats.pdf

-- Crime Reports by District
query: crimes_by_district is crimes -> {
  group_by: District
  aggregate: Reports
}

-- Crime Reports by Ward
query: crimes_by_ward is crimes -> {
  group_by: Ward
  aggregate: Reports
}

-- Crime Reports by Beat
query: crimes_by_beat is crimes -> {
  group_by: Beat
  aggregate: Reports
}
