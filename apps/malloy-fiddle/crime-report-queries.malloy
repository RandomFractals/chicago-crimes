import "./crime-reports.malloy"

// --
// Name: Crime Reports Dashboard
query: crimes_dashboard is crimes -> {
  aggregate: Reports
  nest:
    Domestic_Reports
    Arrests
    crimes_by_month_line_chart
    crimes_by_primary_type
}

// --
// Name: Crime Reports by Primary Type with Nested Description Table Summary
query: crimes_by_primary_type_with_description is
  crimes -> crimes_by_primary_type + {
    nest: crimes_by_description
  }

// --
// Name: Crime Reports by Primary Type with Nested Description Chart Summary
query: crimes_by_primary_type_summary is
  crimes -> crimes_by_primary_type + {
    nest: crimes_by_description_bar_chart is
      crimes_by_description { limit: 10 }
    }

// --
// Name: Crime Reports by Primary Type with Monthly Summary and Top Locations
query: crimes_by_type_month_and_location is crimes -> {
  group_by: `Primary Type`
  aggregate:
    Reports
    percent_of_reports is Reports/all(Reports) * 100
  nest:
    crimes_by_month_line_chart
    top_locations is {
      group_by: `Location Description`
      aggregate:
        Reports
        percent_of_reports is Reports/all(Reports) * 100
      limit: 15
    }
  limit: 20
}

// --
// Name: Reported Bike Thefts Summary
query: bike_thefts is crimes -> {
  where: Description ~ '%BIKE%'
  group_by: `Primary Type`
  aggregate: Reports
  nest:
    crimes_by_month_line_chart
    top_locations is {
      group_by: `Location Description`
      aggregate:
        Reports
        percent_of_reports is Reports/all(Reports) * 100
      limit: 15
    }
    crimes_by_description is {
      group_by: Description
      aggregate: Reports
      limit: 10
    }
  limit: 20
}

// --
// Name: Weapons Violations Summary
query: weapons_violations is crimes -> {
  where: `Primary Type` ~ '%WEAPONS%'
  group_by: `Primary Type`
  aggregate:
    Reports
  nest:
    crimes_by_month_line_chart
    top_locations is {
      group_by: `Location Description`
      aggregate:
        Reports
        percent_of_reports is Reports/all(Reports) * 100
      limit: 15
    }
    crimes_by_description is {
      group_by: Description
      aggregate: Reports
      limit: 10
    }
  limit: 20
}

// --
// Name: Crime Reports by Primary Type Bar Chart
query: crimes_by_primary_type_bar_chart is
  crimes -> crimes_by_primary_type

// --
// Name: Crime Reports by Description Bar Chart
query: crimes_by_description_bar_chart is
  crimes -> crimes_by_description { limit: 20 }

// --
// Name: Crime Reports by Primary Type and Description Summary
query: crimes_by_primary_type_and_description is crimes -> {
  group_by: `Primary Type`, Description
  aggregate: Reports
  limit: 100
}

// --
// Name: Crime Reports by Location Description Bar Chart
query: crimes_by_location_description_bar_chart is
  crimes -> crimes_by_location_description { limit: 20 }
