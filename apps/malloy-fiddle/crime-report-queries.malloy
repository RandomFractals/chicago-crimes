import "./crime-reports.malloy"

// --
// Name: Crime Reports Summary
query: crimes -> {
  aggregate: Reports
  nest:
    reports_by_month_line_chart
    reports_by_primary_type
}

// --
// Name: Crime Reports by Primary Type with Nested Description Table Summary
query: crimes -> reports_by_primary_type + {
  nest: reports_by_description
}

// --
// Name: Crime Reports by Primary Type with Nested Description Chart Summary
query: crimes -> reports_by_primary_type + {
  nest: reports_by_description_bar_chart is
    reports_by_description { limit: 10 }
}

// --
// Name: Crime Reports by Primary Type with Monthly Summary and Top Locations
query: crimes -> {
  group_by: `Primary Type`
  aggregate:
    Reports
    percent_of_reports is Reports/all(Reports) * 100
  nest:
    reports_by_month_line_chart
    top_locations is {
      group_by: `Location Description`
      aggregate:
        Reports
        percent_of_reports is Reports/all(Reports) * 100
      limit: 10
    }
  limit: 20
}

// --
// Name: Reported Bike Thefts Summary
query: crimes -> {
  where: Description ~ '%BIKE%'
  group_by: `Primary Type`
  aggregate: Reports
  nest:
    reports_by_month_line_chart
    top_locations is {
      group_by: `Location Description`
      aggregate:
        Reports
        percent_of_reports is Reports/all(Reports) * 100
      limit: 15
    }
    crimes_by_description is {
      group_by: Description
      aggregate: Reports
      limit: 10
    }
  limit: 20
}

// --
// Name: Weapons Violations Summary
query: crimes -> {
  where: `Primary Type` ~ '%WEAPONS%'
  group_by: `Primary Type`
  aggregate:
    Reports
  nest:
    reports_by_month_line_chart
    top_locations is {
      group_by: `Location Description`
      aggregate:
        Reports
        percent_of_reports is Reports/all(Reports) * 100
      limit: 15
    }
    crimes_by_description is {
      group_by: Description
      aggregate: Reports
      limit: 10
    }
  limit: 20
}

// --
// Name: Crime Reports by Description
query: crimes -> reports_by_description { limit: 20 }

// --
// Name: Crime Reports by Primary Type and Description Summary
query: crimes -> {
  group_by: `Primary Type`, Description
  aggregate: Reports
  limit: 100
}

// --
// Name: Crime Reports by Location Description Summary
query: crimes -> reports_by_location_description { limit: 20 }
